import com.modrinth.minotaur.TaskModrinthUpload

plugins {
    id "com.github.johnrengelman.shadow" version "7.0.0"
    id 'com.matthewprenger.cursegradle'
    id "com.modrinth.minotaur"
}

configurations {
    shadowCommon
    compileClasspath.extendsFrom shadowCommon
    runtimeClasspath.extendsFrom shadowCommon
}

architectury {
    platformSetupLoomIde()
    fabric()
}

repositories {
    maven {
        name = "LuckPerms maven"
        url = 'https://oss.sonatype.org/content/repositories/snapshots'
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_version}"

    // Permission API
    modImplementation 'me.lucko:fabric-permissions-api:0.1-SNAPSHOT'

    include("xyz.nucleoid:disguiselib-fabric:${rootProject.disguiselib_version}")

    //modRuntime "xyz.nucleoid:disguiselib-fabric:${rootProject.disguiselib_version}"
    modRuntime 'com.github.NucleoidMC.DisguiseLib:disguiselib-fabric:13153217f9'

    //modRuntime "com.github.CaffeineMC:hydrogen-fabric:mc1.16.5-v0.2.0"
    modRuntime "com.github.astei:lazydfu:0.1.2"

    developmentFabric(project(path: ":common", configuration: "dev")) {
        transitive = false
    }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) {
        transitive = false
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier "fabric"
}

jar {
    classifier "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                classifier null
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}
def ENV = System.getenv();
curseforge {
    if (ENV.CURSEFORGE_API_KEY) {
        apiKey = ENV.CURSEFORGE_API_KEY
    }

    project {
        id = "481116"
        changelog = "A changelog can be found at https://github.com/samolego/MobDisguises/releases/${version}"
        releaseType = "release"
        addGameVersion "${project.minecraft_version}"
        addGameVersion "Fabric"

        mainArtifact(file("${project.buildDir}/libs/${archivesBaseName}-${version}-fabric.jar")) {
            displayName = "[${project.minecraft_version}] MobDisguises ${version} [Fabric]"
        }

        afterEvaluate {
            uploadTask.dependsOn("remapJar")
        }
    }

    options {
        forgeGradleIntegration = false
    }
}

task modrinth(type: TaskModrinthUpload, dependsOn: remapJar) {
    onlyIf {
        ENV.MODRINTH_TOKEN
    }

    token = ENV.MODRINTH_TOKEN
    projectId = "AnnyO6Mp"
    versionNumber = version+ "-fabric"
    changelog = "A changelog can be found at https://github.com/samolego/MobDisguises/releases/${version}"
    versionName = "[${project.minecraft_version}] MobDisguises ${version} [Fabric]"
    releaseType = "release"

    uploadFile = file("${project.buildDir}/libs/${archivesBaseName}-${version}-fabric.jar")

    addGameVersion("${project.minecraft_version}")
    addLoader('fabric')
}
